plugins {
	id 'application'
	id 'java'
	id 'org.springframework.boot' version '2.7.5'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.github.node-gradle.node' version '3.2.1'
}

group = 'org.zerock'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.security:spring-security-test'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.mysql:mysql-connector-j'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	implementation 'org.modelmapper:modelmapper:3.1.0'

	implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.2'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.2'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.2'

	implementation 'org.springframework.boot:spring-boot-starter-mail'

	//	AWS S3 서비스를 사용하기 위한 라이브러리
	implementation 'com.amazonaws:aws-java-sdk-s3:1.11.238'

	// mybatis 의존성 추가
	implementation 'org.mybatis:mybatis:3.5.9'
	implementation 'org.mybatis:mybatis-spring:2.0.7'

}

tasks.named('test') {
	useJUnitPlatform()
}


node {
	version = '17.6.0'
	npmVersion = '8.5.1'
	download = true
}


task appNpmInstall(type: NpmTask) {
	description = "Installs dependencies from package.json"
	workingDir = file("${projectDir}/src/main/resources/static");
	args = ["install"]
}

processResources{
	dependsOn "appNpmInstall"
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	sourceCompatibility = '17'
	targetCompatibility = '17'
}

application {
	mainClass = 'org.zerock.ziczone.ZiczoneApplication'
}

task buildWithoutTests {
	dependsOn 'assemble'
	description = 'Builds the project without running tests'
	group = 'build'
}